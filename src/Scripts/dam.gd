extends Area2D
class_name Dam

@export var max_health: int = 100
@export var starting_health: int = 100
@export var health_loss_per_day: int = 15

@export var repair_amount: int = 25
@export var cost_exponent: float = 2.0

@export var base_wood_cost: int = 5
@export var base_stone_cost: int = 3

var health: int
var is_destroyed: bool = false


func _ready() -> void:
	health = clamp(starting_health, 0, max_health)

	SignalBus.day_changed.connect(_on_day_changed)


func interact() -> void:
	try_repair()


func _on_day_changed(_day: int) -> void:
	if is_destroyed:
		return

	health -= health_loss_per_day
	health = max(health, 0)

	print(health)
	if health <= 0:
		destroy_dam()


func get_repair_cost() -> Dictionary:
	var damage_ratio: float = 1.0 - (float(health) / max_health)

	var multiplier: float = pow(1.0 + damage_ratio, cost_exponent)

	return {
		GameManager.Item.WOOD: ceil(base_wood_cost * multiplier),
		GameManager.Item.STONE: ceil(base_stone_cost * multiplier)
	}


func can_repair(cost: Dictionary) -> bool:
	if is_destroyed or health >= max_health:
		return false

	for resource in cost:
		if not GameManager.inv_has_item(resource, cost[resource]):
			return false

	return true


func try_repair() -> bool:
	if is_destroyed:
		return false

	if health >= max_health:
		return false

	var cost: Dictionary = get_repair_cost()
	print("Repair cost: ", cost)


	if not can_repair(cost):
		return false

	for resource in cost:
		GameManager.inv_remove_item(resource, cost[resource])

	health = min(health + repair_amount, max_health)

	return true


func destroy_dam() -> void:
	if is_destroyed:
		return

	is_destroyed = true
	health = 0

	# - Trigger game over
	print("Dam destroyed")


func get_health() -> int:
	return health


func get_health_ratio() -> float:
	return float(health) / max_health


func _on_detection_box_body_exited(body: Node2D) -> void:
	if body.is_in_group("player"):
		SignalBus.context_update.emit(self , false)

func _on_detection_box_body_entered(body: Node2D) -> void:
	if body.is_in_group("player"):
		SignalBus.context_update.emit(self , true)